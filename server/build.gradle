plugins {
	id 'java'
	id 'org.springframework.boot' version '3.4.5'
	id 'io.spring.dependency-management' version '1.1.7'
	id "org.openapi.generator" version "7.14.0"
}

group = 'com.server'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-webflux'
	implementation 'org.hibernate.validator:hibernate-validator:9.0.0.Final'

	// docs
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.8'
	implementation 'org.openapitools:jackson-databind-nullable:0.2.6'
	implementation 'javax.annotation:javax.annotation-api:1.3.2'
	implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'

	// db access
	implementation 'org.postgresql:postgresql:42.7.2'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	// db migrations
	implementation 'org.flywaydb:flyway-core'
	implementation 'org.flywaydb:flyway-database-postgresql'
	// tests
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	testImplementation 'org.mockito:mockito-core'

	// observability
	implementation 'io.opentelemetry:opentelemetry-exporter-otlp'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation platform('io.micrometer:micrometer-bom:1.15.0')
	implementation 'io.micrometer:micrometer-tracing'
	implementation 'io.micrometer:micrometer-tracing-bridge-otel'
	implementation 'io.micrometer:micrometer-registry-otlp'

	// lombok => less weird setter/getters
	compileOnly("org.projectlombok:lombok:1.18.38")
	annotationProcessor("org.projectlombok:lombok:1.18.38")
	testCompileOnly("org.projectlombok:lombok:1.18.38")
	testAnnotationProcessor("org.projectlombok:lombok:1.18.38")
}

// Generate GenAI client
openApiGenerate {
    generatorName = "java"
    inputSpec = file("$projectDir/genai.json").exists() ? "$projectDir/genai.json" : "$projectDir/../genai/openapi.json"
    outputDir = "$buildDir/generated-genai"
    apiPackage = "com.server.genai.api"
    modelPackage = "com.server.genai.model"
    configOptions = [
        "library": "webclient",
        "useSpringBoot3": "true",
        "openApiNullable": "false",
        "useJakartaEe": "true",
        "generateApiTests": "false",
        "generateModelTests": "false",
        "generateSupportingFiles": "true",
        "generateApiDocumentation": "false",
        "generateModelDocumentation": "false"
    ]
}

// Generate Summary client
task openApiGenerateSummary(type: org.openapitools.generator.gradle.plugin.tasks.GenerateTask) {
    generatorName = "java"
    inputSpec = file("$projectDir/summary.json").exists() ? "$projectDir/summary.json" : "$projectDir/../summary/openapi.json"
    outputDir = "$buildDir/generated-summary"
    apiPackage = "com.server.summary.api"
    modelPackage = "com.server.summary.model"
    configOptions = [
        "library": "webclient",
        "useSpringBoot3": "true",
        "openApiNullable": "false",
        "useJakartaEe": "true",
        "generateApiTests": "false",
        "generateModelTests": "false",
        "generateSupportingFiles": "true",
        "generateApiDocumentation": "false",
        "generateModelDocumentation": "false"
    ]
}

tasks.named('compileJava') {
    dependsOn 'openApiGenerate', 'openApiGenerateSummary'
}

tasks.named('compileTestJava') {
    dependsOn 'openApiGenerate', 'openApiGenerateSummary'
}

tasks.named('test') {
	useJUnitPlatform()
    dependsOn 'openApiGenerate', 'openApiGenerateSummary'
}

sourceSets {
    main {
        java {
            srcDir "$buildDir/generated-genai/src/main/java"
            srcDir "$buildDir/generated-summary/src/main/java"
        }
    }
    test {
        java {
            srcDir "$buildDir/generated-genai/src/test/java"
            srcDir "$buildDir/generated-summary/src/test/java"
        }
    }
}
